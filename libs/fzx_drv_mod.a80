; -----------------------------------------------------------------------------
; FZX driver - Copyright (c) 2013 Einar Saukas
; FZX format - Copyright (c) 2013 Andrew Owen
; -----------------------------------------------------------------------------
/* fzx_drv_def.a80
        DEFINE  KFzxTab    6
        DEFINE  KFzxAt     22
        DEFINE  KFzxPos    21
        DEFINE  KFzxCR     13
        DEFINE  KFzxScrl   10
        DEFINE  KFzxMargin 7
        DEFINE  KFzxFont   1
        DEFINE  KFzxSpcy   2
        DEFINE  KFzxExe    3
        DEFINE  KFzxExe16  4
        DEFINE  KFzxLGap   5

        define  _orhl   #b6
        define  _andhl  #a6
        define  _xorhl  #ae
*/
        MODULE fzx

MARGIN  EQU     0               ; left margin (in pixels)

init:   ld      hl,(ix+sfzx.TEXT)
        ld      (ix+sfzx.CTXT),hl
        ld      a,(ix+sfzx.MARGIN) : ld (NEWLINE+1),a
        ld      (ix+sfzx.P_COL),a
        ld      a,(ix+sfzx.spcy)   : ld (SPCY+1),a
        ld      (ix+sfzx.P_LIN),191
        ld      (ix+sfzx.P_FLAG),0
        ld      hl,(ix+sfzx.FONT)
        ld      (FONT),hl
        ret

xoroperand:
        ld      a,_xorhl
        jr      changeoperand
andoperand:
        ld      a,_andhl
        jr      changeoperand
oroperand:
        ld      a,_orhl
changeoperand:
        ld      (m1),a
        ld      (m2),a
        ld      (m3),a
        ret

; could not use run due to weird behavior of the compiler v1.13.00
; it replaced rsc.run with rsc.init for no known reason (appeared with rload macro)
runc:   ld      (saveix+2),ix
.load:  ld      hl,(ix+sfzx.FONT) :  ld (FONT),hl
        ld      a,(ix+sfzx.MARGIN):  ld (NEWLINE+1),a
        ld      a,(ix+sfzx.P_COL) :  ld (P_COL),a
        ld      a,(ix+sfzx.P_LIN) :  ld (P_LIN),a
        ld      a,(ix+sfzx.P_FLAG):  ld (P_FLAG),a
        ld      a,(ix+sfzx.spcy)   : ld (SPCY+1),a
        ld      hl,(ix+sfzx.CTXT)

.c0:    ld      bc,(FONT)
        ld      a,(hl)
        cp      #ff
        jp      z,fzret0
        inc     hl

;--- multibyte commands
        cp      KFzxAt                      ; at y,x
        jr      nz,1F
        ld      a,(hl)
        inc     hl
        cpl
        add     a, 192                  ; now A = 191 - char
        ld      (P_LIN),a
        ld      a,(hl)
        inc     hl
        ld      (P_COL),a
        jr      .c0

1       cp      KFzxPos                      ;pos x,y
        jr      nz,1F
        ld      a,(hl) : inc hl : ld (P_COL),a
        ld      a,(hl) : inc hl :
        cpl
        add     a, 192                         ; now A = 191 - char
        ld      (P_LIN),a
        jr      .c0

1       cp      KFzxTab                        ; tab x
        jr      nz,1F
        ld      a,(hl) : inc hl
        ld      (P_COL),a
        jr      .c0

1       cp      KFzxMargin                     ; margin x
        jr      nz,1F
        ld      a,(hl) : inc hl
        ld      (ix+sfzx.MARGIN),a
        ld      (P_COL),a
        ld      (NEWLINE+1),a
        jr      .c0

1       cp      KFzxFont                       ; set font
        jr      nz,1F
        ld      c,(hl) : inc hl
        ld      b,(hl) : inc hl
        ld      (FONT),bc
        ld      (ix+sfzx.FONT),bc
        jr      .c0

1       cp      KFzxSpcy                       ; set spcy
        jr      nz,1F
        ld      a,(hl) : inc hl
        ld      (SPCY+1),a
        jr      .c0

1       cp      KFzxLGap
        jr      nz,1F
        ld      a,(hl) : inc hl
        ld      (vscroll.lineGap),a
        jr      .c0

1       cp      KFzxExe                        ; custom call
        jr      nz,1F
        ld      a,(hl) : inc hl         ; a=parametter passed to exe
2       ld      e,(hl) : inc hl         ; exe address
        ld      d,(hl) : inc hl
        push    hl,ix
        ld      (.exe),de
        call    longCall
        db      o0
.exe:   dw      #5555
        pop     ix,hl
        jp      .c0

1       cp      KFzxExe16
        jr      nz,1F
        ld      c,(hl) : inc hl
        ld      b,(hl) : inc hl
        jr      2B

;--- single byte commands
;- save text pointer, load font pointer
1       ld      (ix+sfzx.CTXT),hl       ; store txt pointer
        push    bc                      ; bc=font
        pop     ix

;       test of enter (new line)
        cp      13
        jp      z,NEWLINE

;       test of scroll command (for end-titles)
        cp      #0a
        jp      z,scroll

;--- processing text character

CHK_CHAR:
        dec     a               ; now A = char - 1
        cp      (ix+2)          ; compare with lastchar
        jr      nc, UNDEF_CHAR
        sub     31              ; now A = char - 32
        jr      nc, PRINT_CHAR
UNDEF_CHAR:
        ld      a, '?'-32       ; print '?' instead of invalid character
PRINT_CHAR:
        inc     a               ; now A = char - 31
        ld      l, a
        ld      h, 0
        ld      d, h
        ld      e, l
        add     hl, hl
        add     hl, de          ; now HL = (char - 31) * 3
        add     hl, bc          ; now HL references offset/kern in char table
        ld      e, (hl)
        inc     hl
        ld      a, (hl)
        and     63
        ld      d, a            ; now DE = offset

        xor     (hl)
        rlca
        rlca
        ld      c, a            ; now C = kern

        push    hl
        add     hl, de
        dec     hl              ; now HL = char definition address
        ex      (sp), hl        ; now HL references offset/kern in char table
        inc     hl              ; now HL references shift/width in char table
        xor     a
        rld                     ; now A = char shift
        push    af
        rld                     ; now A = (width - 1)
        ld      (WIDTH1+1), a
        cp      8               ; check if char width is larger than 8 bits
        rld                     ; restore char shift/width

        ld      de, $000e       ; same as "LD C,0"
        jr      c, NARROW_CHAR
        ld      de, $234e       ; same as "LD C,(HL)" and "INC HL"
NARROW_CHAR:
        ld      (SMC), de       ; self-modify code to handle narrow/large chars

        inc     hl              ; now HL references next char offset
        ld      a, (hl)         ; now A = LSB of next char offset
        add     a, l
        ld      e, a            ; now E = LSB of next char definition address

        ld      a, (P_COL)
        sub     c               ; move left number of pixels specified by kern
        jr      nc, ON_SCREEN   ; stop moving if it would fall outside screen
        xor     a
ON_SCREEN:
        ld      (P_COL), a
        ld      l,a

        ld      a, (WIDTH1+1)   ; now A = (width - 1)
        add     a, l            ; now A = (width - 1) + column
        call    c, NEWLINE      ; if char width won't fit then move to new line

        ld      bc, (P_COL)
        ld      a, 1
        sub     (ix+0)          ; now A = 1 - height
        add     a, b            ; now A = P_LIN - height + 1
;        jp      nc, $0c86       ; call routine REPORT-5 ("Out of screen")
        pop     af              ; now A = shift

        add     a, 191          ; range 0-191
        call    $22aa + 2       ; call PIXEL-ADD + 2 to calculate screen address
        ex      af, af'
                                ; now A' = (col % 8)
        jr      CHK_LOOP

MAIN_LOOP:
        ld      d, (hl)         ; now D = 1st byte from char definition grid
        inc     hl              ; next character definition
SMC:
        ld      c, (hl)         ; now C = 2nd byte from char definition or zero
        inc     hl              ;   (either "LD C,0" or "LD C,(HL)" + "INC HL")
        xor     a               ; now A = zero (since there's no 3rd byte)
        ex      (sp), hl        ; now HL = screen address

        ex      af, af'
                                ; now A = (col % 8), A' = 0
        jr      z, NO_ROTATE
        ld      b, a            ; now B = (col % 8)
        ex      af, af'
                                ; now A = 0, A' = (col % 8)
ROTATE_PIXELS:
        srl     d               ; rotate right char definition grid in D,C,A
        rr      c
        rra
        djnz    ROTATE_PIXELS
NO_ROTATE:
        inc     l
        inc     l
m1:     or      (hl)
        ld      (hl), a         ; put A on screen
        dec     l
        ld      a, c
m2:     or      (hl)
        ld      (hl), a         ; put C on screen
        dec     l
        ld      a, d
m3:     or      (hl)
        ld      (hl), a         ; put D on screen

        inc     h               ; move screen address by 1 pixel down
        ld      a, h
        and     7
        jr      nz, CHK_LOOP
        ld      a, l
        sub     -32
        ld      l, a
        sbc     a, a
        and     -8
        add     a, h
        ld      h, a
CHK_LOOP:
        ex      (sp), hl        ; now HL = char definition address
        ld      a, l
        cp      e               ; check if reached next char definition address
        jr      nz, MAIN_LOOP   ; loop otherwise

        pop     hl              ; discard screen address from stack
        ld      a, (P_COL)      ; now A = column
WIDTH1:
        add     a, 0            ; now A = column + (width - 1)
        scf
        adc     a, (ix+1)       ; now A = column + width + tracking
        ld      (P_COL),a
        jr      nc, EXIT        ; if didn't fall outside the screen then exit
NEWLINE:
        ld      a,MARGIN
        ld      (P_COL),a    ; move to initial column at left margin
        ld      a,(P_LIN)
SPCY:   sub     #55
        sub     (ix+0)          ; now A = line - height
        ld      (P_LIN), a         ; move down a few pixels specified by height
EXIT:
;        ret

        call    saveix
        xor     a
        ret

fzret0: and     a               ; a=#ff, S flag
        scf
        ret

saveix: ld      ix,#5555        ; data of print instance
        ld      hl,(FONT)       : ld (ix+sfzx.FONT),hl
        ld      a,(NEWLINE+1)   : ld (ix+sfzx.MARGIN),a
        ld      a,(P_COL)       : ld (ix+sfzx.P_COL),a
        ld      a,(P_LIN)       : ld (ix+sfzx.P_LIN),a
        ld      a,(P_FLAG)      : ld (ix+sfzx.P_FLAG),a
        ld      a,(SPCY+1)      : ld (ix+sfzx.spcy),a
        ret

scroll_new:
        ld      a,(NEWLINE+1)   : ld (P_COL),a
        call    saveix
        xor     a
        scf
        ret

scroll:
        ld      a,(NEWLINE+1)
        ld      (P_COL),a
        ld      ix,(saveix+2)
        ld      (ix+sfzx.P_COL),a
        xor     a
        scf
        ret

;vscroll.endmark:    db 0
;vscroll.lineGap: db 0            ; temporary if vscroll is not included in main.a80
P_FLAG: defb        0
P_COL:  defb        MARGIN
P_LIN:  defb        191
FONT:   dw          0

; -----------------------------------------------------------------------------
        ENDMODULE